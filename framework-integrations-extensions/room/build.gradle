plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'org.jetbrains.dokka'
    id 'maven-publish'
}

dokkaHtml.configure {
    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
        }
    }
}

tasks.dokkaHtml.configure {
    outputDirectory.set(file("documentation/kotlin"))
}

android {
    compileSdkVersion 30
    buildToolsVersion versions.buildToolsVersion

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes.release.minifyEnabled false
    kotlinOptions.jvmTarget = versions.jvmTarget

    compileOptions {
        sourceCompatibility versions.sourceCompatibility
        targetCompatibility versions.targetCompatibility
    }
}

dependencies {
    implementation jetbrainsKotlinDeps.stdlib
    implementation androidXDeps.coreKtx
    implementation androidXDeps.appCompat
    api roomDeps.runtime
    kapt roomDeps.compiler
    api roomDeps.ktx
    api project(':framework-integrations-persistence')
    testImplementation roomDeps.testHelpers
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = 'com.grarcht.shuttle'
                artifactId = 'framework-integrations-extensions-room'
                version = '1.0.0-alpha01'
                artifact('build/outputs/aar/framework-integrations-extensions-room-release.aar')

                pom.withXml {
                    def dependencies = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        def dependency = dependencies.appendNode('dependency')
                        dependency.appendNode('groupId', it.group)
                        dependency.appendNode('artifactId', it.name)
                        dependency.appendNode('version', it.version)
                    }
                }
            }
            debug(MavenPublication) {
                groupId = 'com.grarcht.shuttle'
                artifactId = 'framework-integrations-extensions-room-debug'
                version = '1.0.0-alpha01'
                artifact('build/outputs/aar/framework-integrations-extensions-room-debug.aar')

                pom.withXml {
                    def dependencies = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        def dependency = dependencies.appendNode('dependency')
                        dependency.appendNode('groupId', it.group)
                        dependency.appendNode('artifactId', it.name)
                        dependency.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}