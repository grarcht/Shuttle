plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'org.jetbrains.dokka'
    id 'maven-publish'
}

dokkaHtml.configure {
    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
        }
    }
}

tasks.dokkaHtml.configure {
    outputDirectory.set(file("documentation/kotlin"))
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes.release.minifyEnabled false
    kotlinOptions.jvmTarget = versions.jvmTarget

    compileOptions {
        sourceCompatibility versions.sourceCompatibility
        targetCompatibility versions.targetCompatibility
    }
}

dependencies {
    implementation jetbrainsKotlinDeps.stdlib
    implementation androidXDeps.coreKtx
    implementation androidXDeps.material
    implementation androidXDeps.appCompat
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = 'com.grarcht.shuttle'
                artifactId = 'framework-integrations-persistence'
                version = '1.0.0-alpha01'
                artifact('build/outputs/aar/framework-integrations-persistence-release.aar')

                pom.withXml {
                    def dependencies = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        def dependency = dependencies.appendNode('dependency')
                        dependency.appendNode('groupId', it.group)
                        dependency.appendNode('artifactId', it.name)
                        dependency.appendNode('version', it.version)
                    }
                }
            }
            debug(MavenPublication) {
                groupId = 'com.grarcht.shuttle'
                artifactId = 'framework-integrations-persistence-debug'
                version = '1.0.0-alpha01'
                artifact('build/outputs/aar/framework-integrations-persistence-debug.aar')

                pom.withXml {
                    def dependencies = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        def dependency = dependencies.appendNode('dependency')
                        dependency.appendNode('groupId', it.group)
                        dependency.appendNode('artifactId', it.name)
                        dependency.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}