plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'org.jetbrains.dokka'
    id 'de.mannodermaus.android-junit5'
    id 'maven-publish'
}

apply from: '../detekt/detekt.gradle'

dokkaHtml.configure {
    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
        }
    }
}

tasks.dokkaHtml.configure {
    outputDirectory.set(file("documentation/kotlin"))
}

android {
    compileSdkVersion 30
    buildToolsVersion versions.buildToolsVersion

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArgument("runnerBuilder", "de.mannodermaus.junit5.AndroidJUnit5Builder")
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes.release.minifyEnabled false
    testOptions.unitTests.returnDefaultValues = true
    kotlinOptions.jvmTarget = versions.jvmTarget

    compileOptions {
        sourceCompatibility versions.sourceCompatibility
        targetCompatibility versions.targetCompatibility
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.md'
        exclude 'META-INF/LICENSE-notice.md'
    }
}

dependencies {
    implementation jetbrainsKotlinDeps.stdlib
    implementation jetbrainsKotlinDeps.coroutines
    implementation androidXDeps.coreKtx
    implementation androidXDeps.appCompat
    implementation androidXDeps.lifecycle.extensions
    api project(':framework-integrations-persistence')
    androidTestImplementation testingDeps.junit.junit5AndroidTestCore
    androidTestRuntimeOnly testingDeps.junit.junit5AndroidTestRunner
    testImplementation testingDeps.mockito.core
    testImplementation testingDeps.mockito.kotlin
    testImplementation testingDeps.junit.jupiterApi
    testRuntimeOnly testingDeps.junit.jupiterEngine
    testImplementation testingDeps.kotlin.coroutines
    testImplementation testingDeps.junit.androidCore
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = 'com.grarcht.shuttle'
                artifactId = 'framework'
                version = '1.0.0-alpha01'
                artifact('build/outputs/aar/framework-release.aar')

                pom.withXml {
                    def dependencies = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        def dependency = dependencies.appendNode('dependency')
                        dependency.appendNode('groupId', it.group)
                        dependency.appendNode('artifactId', it.name)
                        dependency.appendNode('version', it.version)
                    }
                }
            }
            debug(MavenPublication) {
                groupId = 'com.grarcht.shuttle'
                artifactId = 'framework-debug'
                version = '1.0.0-alpha01'
                artifact('build/outputs/aar/framework-debug.aar')

                pom.withXml {
                    def dependencies = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        def dependency = dependencies.appendNode('dependency')
                        dependency.appendNode('groupId', it.group)
                        dependency.appendNode('artifactId', it.name)
                        dependency.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}