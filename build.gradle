buildscript {
    apply from: 'ext.gradle'

    repositories {
        // Instead of using the gogole() repo function, specify the maven url instead.  There is
        // an issue with the hilt dependencies where there are extra dependencies that are pulled
        // in via the gradle module file.  Using the POM file as the source of truth fixes that
        // issue.  This is now being universally applied for all Google artifacts.
        maven {
            url 'https://maven.google.com/'
            metadataSources {
                mavenPom()
                artifact()
                ignoreGradleMetadataRedirection()
            }
        }
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath classpathDeps.gradleBuildTools
        classpath classpathDeps.kotlinGradlePlugin
        classpath classpathDeps.dokkaGradlePlugin
        //https://github.com/mannodermaus/android-junit5
        classpath classpathDeps.junit5Plugin
        classpath dependencyInjectionDeps.hilt
        classpath staticAnalysisDeps.detektPlugin
        classpath dependencyInjectionDeps.hiltPlugin
    }
}

plugins {
    id('com.github.ben-manes.versions').version('0.42.0')
    id('io.gitlab.arturbosch.detekt').version('1.19.0')
    id("org.jetbrains.dokka").version "1.6.10"
}

repositories {
    // Instead of using the gogole() repo function, specify the maven url instead.  There is
    // an issue with the hilt dependencies where there are extra dependencies that are pulled
    // in via the gradle module file.  Using the POM file as the source of truth fixes that
    // issue.  This is now being universally applied for all Google artifacts.
    maven {
        url 'https://maven.google.com/'
        metadataSources {
            mavenPom()
            artifact()
            ignoreGradleMetadataRedirection()
        }
    }
    mavenCentral()
    gradlePluginPortal()
}

detekt {
    toolVersion = "1.19.0"
    config = files("detekt/detekt.yml")
    buildUponDefaultConfig = true
}

dependencies {
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.19.0"
}

def rootDir = projectDir.absolutePath

allprojects {
    apply from: "$rootDir/ext.gradle"
    apply from: "$rootDir/detekt/detekt.gradle"

    repositories {
        mavenLocal()

        // Instead of using the gogole() repo function, specify the maven url instead.  There is
        // an issue with the hilt dependencies where there are extra dependencies that are pulled
        // in via the gradle module file.  Using the POM file as the source of truth fixes that
        // issue.  This is now being universally applied for all Google artifacts.
        maven {
            url 'https://maven.google.com/'
            metadataSources {
                mavenPom()
                artifact()
                ignoreGradleMetadataRedirection()
            }
        }

        // The Shuttle framework artifacts are hosted in Sonatype Nexus Maven Central.
        maven { url 'https://repo1.maven.org/maven2/' }

        mavenCentral()
        gradlePluginPortal()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
