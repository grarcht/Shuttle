buildscript {
    apply from: 'ext.gradle'

    repositories {
        mavenCentral()

        // Instead of using the google() repo function, specify the maven url instead.  There is
        // an issue with the hilt dependencies where there are extra dependencies that are pulled
        // in via the gradle module file.  Using the POM file as the source of truth fixes that
        // issue.  This is now being universally applied for all Google artifacts.
        maven {
            url 'https://maven.google.com/'
            metadataSources {
                //noinspection ForeignDelegate
                mavenPom()
                //noinspection ForeignDelegate
                artifact()
                //noinspection ForeignDelegate
                ignoreGradleMetadataRedirection()
            }
        }
        gradlePluginPortal()
    }
    dependencies {
        classpath libs.classpathDeps.gradleBuildTools
        classpath libs.classpathDeps.kotlinGradlePlugin
        classpath libs.classpathDeps.dokkaGradlePlugin
        //https://github.com/mannodermaus/android-junit5
        classpath libs.classpathDeps.junit5Plugin
        classpath libs.dependencyInjectionDeps.hilt
        classpath libs.staticAnalysisDeps.detektPlugin
        classpath libs.dependencyInjectionDeps.hiltPlugin
    }
}

plugins {
    alias libs.plugins.android.application apply false
    alias libs.plugins.android.library apply false
    alias libs.plugins.google.dagger.hilt apply false
    alias libs.plugins.detect
    alias libs.plugins.jetbrains.dokka
    alias libs.plugins.google.ksp apply false
    alias libs.plugins.jetbrains.kotlin.android apply false
    alias(libs.plugins.compose.compiler) apply false
}

apply from: "$rootDir/detekt/detekt.gradle"

repositories {
    mavenCentral()

    // Instead of using the google() repo function, specify the maven url instead.  There is
    // an issue with the hilt dependencies where there are extra dependencies that are pulled
    // in via the gradle module file.  Using the POM file as the source of truth fixes that
    // issue.  This is now being universally applied for all Google artifacts.
    maven {
        url 'https://maven.google.com/'
        metadataSources {
            //noinspection ForeignDelegate
            mavenPom()
            //noinspection ForeignDelegate
            artifact()
            //noinspection ForeignDelegate
            ignoreGradleMetadataRedirection()
        }
    }
    gradlePluginPortal()
}


def rootDir = projectDir.absolutePath

allprojects {
    apply from: "$rootDir/ext.gradle"
    apply from: "$rootDir/detekt/detekt.gradle"

    repositories {
        mavenLocal()

        // The Shuttle framework artifacts are hosted in Sonatype Nexus Maven Central.
        maven {
            url 'https://repo1.maven.org/maven2/com/grarcht/shuttle/'
            metadataSources {
                //noinspection ForeignDelegate
                mavenPom()
                //noinspection ForeignDelegate
                artifact()
                //noinspection ForeignDelegate
                ignoreGradleMetadataRedirection()
            }
        }

        google()

        mavenCentral()

        // Instead of using the google() repo function, specify the maven url instead.  There is
        // an issue with the hilt dependencies where there are extra dependencies that are pulled
        // in via the gradle module file.  Using the POM file as the source of truth fixes that
        // issue.  This is now being universally applied for all Google artifacts.
        maven {
            url 'https://maven.google.com/'
            metadataSources {
                //noinspection ForeignDelegate
                mavenPom()
                //noinspection ForeignDelegate
                artifact()
                //noinspection ForeignDelegate
                ignoreGradleMetadataRedirection()
            }
        }

        gradlePluginPortal()
    }
}

tasks.register("clean", Delete) {
    delete rootProject.layout.buildDirectory
}
