plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'org.jetbrains.dokka'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-kapt'
    id 'de.mannodermaus.android-junit5'
}

apply from: '../../detekt/detekt.gradle'

dokkaHtml.configure {
    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
        }
    }
}

tasks.dokkaHtml.configure {
    outputDirectory.set(file("documentation/kotlin"))
}

android {
    compileSdkVersion 31
    buildToolsVersion versions.buildToolsVersion

    defaultConfig {
        applicationId 'com.grarcht.shuttle'
        minSdkVersion 26
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArgument("runnerBuilder", "de.mannodermaus.junit5.AndroidJUnit5Builder")
    }

    kotlinOptions.jvmTarget = versions.jvmTarget

    compileOptions {
        sourceCompatibility versions.sourceCompatibility
        targetCompatibility versions.targetCompatibility
    }

    buildFeatures {
        compose true
        dataBinding true
        viewBinding true
    }

    composeOptions {
        kotlinCompilerExtensionVersion versions.kotlinCompilerExt
    }

    kapt {
        correctErrorTypes true
        javacOptions {
            // https://youtrack.jetbrains.com/issue/KT-47416
            // https://github.com/nvmnghia/yacv/commit/7f85c02e3d3d965fd780f130af802c73822699df
            // These options are normally set automatically via the Hilt Gradle plugin, but we
            // set them manually to workaround a bug in the Kotlin 1.5.20
            option("-Adagger.fastInit=ENABLED")
            option("-Adagger.hilt.android.internal.disableAndroidSuperclassValidation=true")
        }
    }
}

dependencies {
    implementation jetbrainsKotlinDeps.stdlib
    implementation jetbrainsKotlinDeps.coroutines

    implementation androidXDeps.activityKtx
    implementation androidXDeps.appCompat
    implementation androidXDeps.compose.activity
    implementation androidXDeps.compose.foundation
    implementation androidXDeps.compose.foundationLayout
  //  implementation androidXDeps.compose.foundationText
    implementation androidXDeps.compose.material
    implementation androidXDeps.compose.materialIcons
    implementation androidXDeps.compose.runtime
    implementation androidXDeps.compose.ui
    implementation androidXDeps.compose.uiTooling
    implementation androidXDeps.constraintLayout
    implementation androidXDeps.coreKtx
    implementation androidXDeps.lifecycle.extensions
    implementation androidXDeps.lifecycle.viewModel
    implementation androidXDeps.material

    api dependencyInjectionDeps.hilt
    api dependencyInjectionDeps.hiltViewModel
   // implementation 'androidx.compose.foundation:foundation:1.0.0-beta04'
    //implementation 'androidx.compose.material:material:1.0.0-beta04'
    kapt dependencyInjectionDeps.hiltCompiler

    implementation project(':demos-core-lib')
    implementation project(':demos-databinding-lib')


    // To use a single aar file dependency, do something like the following.
    // If another integration extension is desired, a corresponding configuration should be
    // declared here for the "solution-builder" project.
    // implementation project(path: ':solution-builder', configuration: 'withRoom')

    // To use lighter weight, independent dependencies, comment out the above dependency and
    // uncomment the following:
    // implementation project(':framework')
    // implementation project(':framework-integrations-persistence')
    // implementation project(':framework-integrations-extensions-room')
    // implementation project(':framework-addons-navigation-component')

    // To use maven dependencies, use the following:
    implementation shuttle.framework
    implementation shuttle.integrationsExtensionsRoom
    implementation shuttle.addOnsNavigationComponent

    androidTestImplementation testingDeps.junit.ext
    androidTestImplementation testingDeps.espresso.core
    androidTestRuntimeOnly testingDeps.junit.junit5AndroidTestRunner
    testImplementation testingDeps.junit.jupiterApi
    testRuntimeOnly testingDeps.junit.jupiterEngine
}