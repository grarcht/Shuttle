plugins {
    alias libs.plugins.android.application
    alias libs.plugins.kotlin.android
    alias libs.plugins.jetbrains.dokka
    alias libs.plugins.google.dagger.hilt
    alias libs.plugins.google.ksp
    alias libs.plugins.android.junit5
    alias libs.plugins.jetbrains.kotlin.android
}

apply from: '../../detekt/detekt.gradle'

dokkaHtml {
    outputDirectory.set(file("documentation/kotlin"))
}

android {
    namespace 'com.grarcht.shuttle.demo.mvvmwithnavigation'

    defaultConfig {
        applicationId 'com.grarcht.shuttle.demo.mvvm_with_navigation'
        compileSdk libs.versions.compileSdk.get().toInteger()
        minSdk libs.versions.minSdk.get().toInteger()
        targetSdk libs.versions.targetSdk.get().toInteger()
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArgument("runnerBuilder", "de.mannodermaus.junit5.AndroidJUnit5Builder")
    }

    kotlinOptions {
        jvmTarget = libs.versions.jvmTarget.get()
    }

    compileOptions {
        sourceCompatibility libs.versions.sourceCompatibility.get()
        targetCompatibility libs.versions.targetCompatibility.get()
    }

    buildFeatures {
        viewBinding true
    }

    def dependenciesType = "dependenciesType"
    flavorDimensions += dependenciesType

    productFlavors {
        artifacts { dimension dependenciesType }
        modules { dimension dependenciesType }
        solutionBuilder { dimension dependenciesType }
    }
}

dependencies {
    implementation libs.jetbrainsKotlinDeps.stdlib
    implementation libs.jetbrainsKotlinDeps.coroutines
    implementation libs.android.annotationJvm
    implementation libs.android.appCompat
    implementation libs.android.constraintLayout
    implementation libs.android.material
    implementation libs.android.coreKtx
    implementation libs.android.fragmentKtx
    implementation libs.android.lifecycle.extensions
    implementation libs.android.lifecycle.viewModel
    implementation libs.android.navigationFragmentKtx

    api libs.dependencyInjectionDeps.hilt
    ksp libs.dependencyInjectionDeps.hiltCompiler

    implementation project(':demos-core-lib')

    // To use a single pom / gradle module, do something like the following.
    solutionBuilderImplementation project(path: ':solution-builder', configuration: 'withRoom')

    // Lighter weight, independent dependencies
    modulesImplementation project(':framework')
    modulesImplementation project(':framework-integrations-persistence')
    modulesImplementation project(':framework-integrations-extensions-room')
    modulesImplementation project(':framework-addons-navigation-component')

    // To use maven dependencies, use the following:
    artifactsImplementation libs.shuttle.framework
    artifactsImplementation libs.shuttle.integrationsPersistence
    artifactsImplementation libs.shuttle.integrationsExtensionsRoom
    artifactsImplementation libs.shuttle.addOnsNavigationComponent

    androidTestImplementation libs.testingDeps.junit.ext
    androidTestImplementation libs.testingDeps.espresso.core
    androidTestRuntimeOnly libs.testingDeps.junit.junit5AndroidTestRunner
    testImplementation libs.testingDeps.junit.jupiterApi
    testRuntimeOnly libs.testingDeps.junit.jupiterEngine
}